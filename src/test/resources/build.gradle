buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath('org.springframework.boot:spring-boot-gradle-plugin:1.4.3.RELEASE')
    }
}

apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'

tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }
tasks.withType(GroovyCompile) { options.encoding = 'UTF-8' }

group = 'com.blackducksoftware.integration'
version = '2.1.1-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'com.blackducksoftware.integration:hub-common:11.0.0-SNAPSHOT'

    compile 'org.springframework.boot:spring-boot-starter'
    compile 'org.springframework:spring-web'
    compile 'org.codehaus.groovy:groovy-all:2.4.7'
    compile 'org.apache.commons:commons-compress:1.13'

    compileOnly 'org.artifactory:artifactory-papi:4.14.0'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

eclipse.project { natures 'org.springsource.ide.eclipse.gradle.core.nature' }

springBoot { mainClass = 'com.blackducksoftware.integration.hub.artifactory.Application' }

task produceFinalZip(type: Zip, dependsOn: build) {
    from("${buildDir}/libs") { include '*.jar' }
    from("${projectDir}/src/main/groovy/com/blackducksoftware/integration/hub/artifactory/scan") {
        include 'blackDuckScanForHub.groovy'
        into 'scanner'
    }
    from(configurations.runtime) {
        exclude 'artifactory-papi*.jar'
        exclude 'commons-compress*.jar'
        exclude 'groovy-all*.jar'
        exclude 'gson*.jar'
        exclude 'logback*.jar'
        exclude 'snakeyaml*.jar'
        exclude 'spring*.jar'
        into 'scanner/lib'
    }
}
