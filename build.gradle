import com.synopsys.integration.blackduck.api.generated.view.ProjectVersionView
import com.synopsys.integration.blackduck.api.generated.view.VersionBomComponentView
import com.synopsys.integration.blackduck.configuration.BlackDuckServerConfig
import com.synopsys.integration.blackduck.configuration.BlackDuckServerConfigBuilder
import com.synopsys.integration.blackduck.service.BlackDuckService
import com.synopsys.integration.blackduck.service.BlackDuckServicesFactory
import com.synopsys.integration.blackduck.service.ProjectService
import com.synopsys.integration.blackduck.service.model.ProjectVersionWrapper
import com.synopsys.integration.log.PrintStreamIntLogger

buildscript {
    ext {
        springBootVersion = '2.1.5.RELEASE'
        blackDuckCommonVersion = '44.2.14'
        polarisCommonVersion = '0.13.2'
    }
    repositories {
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath 'com.blackducksoftware.integration:common-gradle-plugin:0.0.+'
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "com.blackducksoftware.integration:blackduck-common:${blackDuckCommonVersion}"
        classpath "com.synopsys.integration:polaris-common:${polarisCommonVersion}"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.31' apply false
}

group = 'com.synopsys.integration'
version = '6.1.0-SNAPSHOT'

apply plugin: 'com.blackducksoftware.integration.solution'
apply plugin: 'org.springframework.boot'
apply from: 'artifactory-properties.gradle'
apply from: 'docs.gradle'

def createArtifactName() {
    return "${buildDir}/libs/${project.name}-${version}.jar"
}

allprojects {
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'io.spring.dependency-management'

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        implementation "org.jetbrains.kotlin:kotlin-reflect"
        implementation "com.blackducksoftware.integration:blackduck-common:${blackDuckCommonVersion}"
        implementation 'com.paypal.digraph:digraph-parser:1.0'
        implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.0'
        implementation 'com.fasterxml.jackson.core:jackson-core:2.10.0'
    }
}

subprojects {
    group = rootProject.group
    version = rootProject.version
    apply plugin: 'com.blackducksoftware.integration.simple'

    license {
        header rootProject.file('LICENSE')
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            def jarName = createArtifactName()
            artifact(jarName)
        }
    }
}

artifactory {
    publish {
        defaults { publications('mavenJava') }
    }
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

dependencies {
    implementation project(":detect-configuration")
    implementation project(":detectable")
    implementation project(":detector")

    implementation "org.zeroturnaround:zt-zip:1.13"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "com.synopsys.integration:polaris-common:${polarisCommonVersion}"
    implementation 'org.yaml:snakeyaml:1.24'

    implementation 'org.apache.maven.shared:maven-invoker:3.0.0'
    implementation 'com.esotericsoftware.yamlbeans:yamlbeans:1.11'
    implementation 'com.moandjiezana.toml:toml4j:0.7.1'
    implementation 'org.freemarker:freemarker:2.3.26-incubating'
    implementation 'org.springframework.boot:spring-boot-starter'

    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.8'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

springBoot { mainClassName = 'com.synopsys.integration.detect.Application' }

if ("true" == project.findProperty('refresh.cache')) {
    project.dependencies {
        implementation 'com.blackducksoftware.integration:integration-gradle-inspector:0.6.0'
    }

    project.repositories.clear()
    project.repositories {
        maven {
            url 'https://repo.blackducksoftware.com:443/artifactory/bds-integration-public-cache'
            credentials {
                username = project.ext.artifactoryDeployerUsername
                password = project.ext.artifactoryDeployerPassword
            }
        }
    }
}

task runDetect(type: JavaExec) {
    dependsOn build

    classpath = files(createArtifactName())
    classpath += sourceSets.main.runtimeClasspath
    main = 'com.synopsys.integration.detect.Application'
    args = ['--detect.risk.report.pdf=true']
}

task verifyBlackDuckDetect() {
    dependsOn runDetect

    doLast {
        BlackDuckServerConfigBuilder blackDuckServerConfigBuilder = BlackDuckServerConfig.newBuilder()
        Set<String> environmentKeys = blackDuckServerConfigBuilder.getEnvironmentVariableKeys()
        environmentKeys.each {
            String value = System.getenv().get(it)
            if (value) {
                blackDuckServerConfigBuilder.setProperty(it, value)
            }
        }

        BlackDuckServerConfig blackDuckServerConfig = blackDuckServerConfigBuilder.build()
        BlackDuckServicesFactory blackDuckServicesFactory = blackDuckServerConfig.createBlackDuckServicesFactory(new PrintStreamIntLogger(System.out, com.synopsys.integration.log.LogLevel.INFO))
        BlackDuckService blackDuckService = blackDuckServicesFactory.createBlackDuckService()
        ProjectService projectService = blackDuckServicesFactory.createProjectService()

        ProjectVersionWrapper projectVersionWrapper = projectService.getProjectVersion(project.name, version).get()
        List<VersionBomComponentView> bomComponents = blackDuckService.getAllResponses(projectVersionWrapper.projectVersionView, ProjectVersionView.COMPONENTS_LINK_RESPONSE)
        if (bomComponents.isEmpty()) {
            throw new GradleException("No bom components were found for ${project.name} - ${version}");
        }
        bomComponents.each {
            println "${it.componentName} - ${it.componentVersionName}"
        }
    }

    task verifyDetect() {
        dependsOn verifyBlackDuckDetect
    }
}

String parseAirGapZipPath(ByteArrayOutputStream standardOutput) {
    String line = standardOutput.toString().split('\n').find { it.contains("Detect Air Gap Zip: ") }
    String path = line.substring(line.lastIndexOf("Detect Air Gap Zip: ") + "Detect Air Gap Zip: ".length()).trim()
    println "Found Air Gap Zip: " + path
    return path
}

task createAirGapZip(type: JavaExec) {
    dependsOn build

    classpath = files(createArtifactName())
    classpath += sourceSets.main.runtimeClasspath
    main = 'com.synopsys.integration.detect.Application'
    args = ["--detect.output.path=${buildDir}/libs/", '-z']
    standardOutput = new ByteArrayOutputStream()
    doLast {
        createAirGapZip.ext.airGapPath = parseAirGapZipPath(standardOutput)
    }
}

task publishAirGapZip() {
    dependsOn createAirGapZip

    doLast {
        def airGapZipName = "${project.name}-${version}-air-gap.zip"
        def airGapZipLocation = createAirGapZip.airGapPath
        exec {
            commandLine 'curl', '--insecure', '-u', "${project.ext.artifactoryDeployerUsername}:${project.ext.artifactoryDeployerPassword}", '-X', 'PUT',
                    "${project.ext.deployArtifactoryUrl}/${project.ext.artifactoryRepo}/com/synopsys/integration/${project.name}/${version}/${airGapZipName}", '-T', "${airGapZipLocation}", '-f'
        }
    }
}

task createAirGapZipWithoutDocker(type: JavaExec) {
    dependsOn build

    classpath = files(createArtifactName())
    classpath += sourceSets.main.runtimeClasspath
    main = 'com.synopsys.integration.detect.Application'
    args = ['--detect.gradle.path=gradlew', '-z', 'GRADLE,NUGET']
    standardOutput = new ByteArrayOutputStream()
    doLast {
        createAirGapZipWithoutDocker.ext.airGapPath = parseAirGapZipPath(standardOutput)
    }

}

task publishAirGapZipWithoutDocker() {
    dependsOn createAirGapZipWithoutDocker

    doLast {
        def airGapZipName = "${project.name}-${version}-air-gap-gradle-nuget.zip"
        def airGapZipLocation = createAirGapZipWithoutDocker.airGapPath
        exec {
            commandLine 'curl', '--insecure', '-u', "${project.ext.artifactoryDeployerUsername}:${project.ext.artifactoryDeployerPassword}", '-X', 'PUT',
                    "${project.ext.deployArtifactoryUrl}/${project.ext.artifactoryRepo}/com/synopsys/integration/${project.name}/${version}/${airGapZipName}", '-T', "${airGapZipLocation}", '-f'
        }
    }
}
